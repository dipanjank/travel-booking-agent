resource "random_password" "db" {
  length  = 24
  special = false
}

resource "aws_db_subnet_group" "database" {
  name       = "${local.project_name}-db-subnet-group"
  subnet_ids = module.vpc.private_subnets

  tags = local.tags
}

resource "aws_security_group" "database" {
  name        = "${local.project_name}-database-sg"
  description = "Allow inbound access to the database from the VPC"
  vpc_id      = module.vpc.vpc_id

  ingress {
    description = "PostgreSQL from VPC"
    from_port   = 5432
    to_port     = 5432
    protocol    = "tcp"
    cidr_blocks = [module.vpc.vpc_cidr_block]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = local.tags
}

module "rds" {
  source  = "terraform-aws-modules/rds/aws"
  version = "~> 6.0"

  identifier = "${local.project_name}-db"

  engine               = "postgres"
  engine_version       = "17"
  family               = "postgres17"
  major_engine_version = "17"
  instance_class       = "db.t4g.micro"

  allocated_storage     = 10
  max_allocated_storage = 20

  db_name  = replace(local.project_name, "-", "_")
  username = "${replace(local.project_name, "-", "_")}_admin"
  port     = 5432

  manage_master_user_password = false
  password                    = random_password.db.result

  multi_az               = false
  db_subnet_group_name   = aws_db_subnet_group.database.name
  vpc_security_group_ids = [aws_security_group.database.id]

  backup_retention_period = 1
  skip_final_snapshot     = true
  deletion_protection     = false

  performance_insights_enabled = false
  create_cloudwatch_log_group  = false

  tags = local.tags
}
